plugins {
    id 'dev.architectury.loom' version '1.7-SNAPSHOT' apply false
    id 'architectury-plugin' version '3.4-SNAPSHOT'
    id 'com.github.johnrengelman.shadow' version '8.1.1' apply false
}

architectury {
    minecraft = project.minecraft_version
}

allprojects {
    group = rootProject.maven_group
    version = rootProject.mod_version
}

subprojects {
    apply plugin: 'dev.architectury.loom'
    apply plugin: 'architectury-plugin'
    apply plugin: 'maven-publish'

    def modLoader = project.name
    boolean isCommon = modLoader == "common"

    base {
        // Set up a suffixed format for the mod jar names, e.g. `example-fabric`.
        archivesName = "$rootProject.archives_name-$project.name"
    }

    repositories {
        mavenCentral()
        maven { url "https://maven.terraformersmc.com/releases" }

    }

    dependencies {
        minecraft "net.minecraft:minecraft:$rootProject.minecraft_version"
        mappings loom.layered() {
            officialMojangMappings()
            parchment("org.parchmentmc.data:parchment-${rootProject.minecraft_version}:${rootProject.parchment_mapping_version}@zip")
        }

        include implementation("com.electronwill.night-config:core:$nightconfig_version")
        include implementation ("com.electronwill.night-config:json:$nightconfig_version")
        include implementation ("com.electronwill.night-config:toml:$nightconfig_version")
        include implementation ("com.electronwill.night-config:yaml:$nightconfig_version")
        include implementation ("com.electronwill.night-config:hocon:$nightconfig_version")

    }

    java {
        // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
        // if it is present.
        // If you remove this line, sources will not be generated.
        withSourcesJar()

        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 21
    }

    // Configure Maven publishing.
    publishing {
        publications {
            mavenJava(MavenPublication) {
                groupId "fr.tathan.tathan-config"
                artifactId "tathan-config-$modLoader-" + minecraft_version
                version project.version
                from components.java

            }
        }

        repositories {
            maven {
                url = "https://maven.odysseyus.fr/snapshots/"
                credentials {
                    username = System.getenv("MAVEN_USER")
                    password = System.getenv("MAVEN_PASS")
                }
            }
        }
    }
}
